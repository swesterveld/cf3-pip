### packages.cf
#
# Packages to install are defined in an array, with desired version numbers:
# "pkgadd[<package>]" string => "<version>";
#
# For example:
#
# "pkgadd[yolk]"    string  => "0.4.3";
# "pkgadd[IPy]"     string  => "0.75";
#
# Location of our virtual environments is saved in variable VIRTUALENVS.

bundle agent pip_example
{
    vars:

        "VIRTUALENVS"   string  => "/data/virtualenvs/";

        "pkgadd[yolk]"  string  => "0.4.3";
        "pkgadd[IPy]"   string  => "0.75";

        "pip_install"   slist   => getindices("pkgadd");

        "pip_delete"    slist   => {
            "Django",
            "pypid",
        };


    packages:

        any::

            "$(pip_install)"

                comment => "Install a Python package in a virtual environment",

                package_method => sara_pip_virtualenv("$(VIRTUALENVS)/test"),
                package_policy => "add",
                package_select => "==",
                package_version => "$(pkgadd[$(pip_install)])";

            "$(pip_delete)"

                comment => "Delete a Python package from a virtual environment",

                package_method => sara_pip_virtualenv("$(VIRTUALENVS)/test"),
                package_policy => "delete";
}
